import tfdownload.OsUtil
import tfdownload.S3Util

repositories {
  ivy {
    url 'https://releases.hashicorp.com/'
    patternLayout {
      /* Terraform release website uses a different layout than standard maven
         structure (underscores insted of dashes and no POM file). */
      artifact(
        '/[organization]/[revision]/[module]_[revision]_[classifier].[ext]' )
    }
    metadataSources { 
      // this tells Gradle only the artifact is there, no other metadata files
      artifact()
    }
  }
}
  
configurations {
  tfBinary
}

dependencies {
  tfBinary "terraform:terraform:1.1.4:${OsUtil.classifier}@zip"
}

ext {
}

/* Intended to be run once, manually, to bootstrap the AWS account - not to 
be a task dependency or something like that. */
task createS3StateBucket {
  description = "creates S3 bucket to store TF state if it doesn't exist"
  ext{
    // must match the AWS TF setup
    bucketName = "tf-download-state"
  }
  doLast {
    def bucket = S3Util.getBucket(ext.bucketName)
    if( bucket ){
      println "ts state bucket exists: $ext.bucketName"
    }
    else {
      println "creating ts state bucket: $ext.bucketName"
      S3Util.createPrivateVersionedBucket(ext.bucketName)
    }
  }
}

//<editor-fold defaultstate="collapsed" desc="TF tasks">

task showTfBinaryLocation {
  doLast {
    println "Terraform binary: " + configurations.tfBinary.singleFile
  }
}

task unzipTfBinary(type: Copy){
  from zipTree(configurations.tfBinary.singleFile)
  into configurations.tfBinary.singleFile.parentFile
}

task tfVersion(type: Exec){
  dependsOn(unzipTfBinary)
  commandLine "${configurations.tfBinary.singleFile.parentFile}/terraform", 
    'version'
}

task tfHelp(type: Exec){
  dependsOn(unzipTfBinary)
  commandLine "${configurations.tfBinary.singleFile.parentFile}/terraform", 
    '-help'
}

task tfInit(type: Exec){
  dependsOn(unzipTfBinary)
  commandLine "${configurations.tfBinary.singleFile.parentFile}/terraform", 
    'init'
}

task tfShow(type: Exec){
  dependsOn(unzipTfBinary)
  commandLine "${configurations.tfBinary.singleFile.parentFile}/terraform", 
    'show'
}

//</editor-fold> "TF tasks"

